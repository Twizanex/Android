apply plugin: 'android'

repositories {
    mavenCentral()
}

def makeRosterUrl(url) {
    return "http://grfx.cstv.com/schools/" + url + "/data/xml/roster/m-footbl-2013.xml"
}

def makeScheduleUrl(url) {
    return "http://cbs-amp2.silverchalice.co/cbs/api/events?schoolCode=" + url + "&sportCode=m-footbl"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as int
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
    }

    signingConfigs {
        myConfig {
            storeFile file(project.signingStoreLocation as String)
            storePassword project.signingStorePassword as String
            keyAlias project.signingKeyAlias as String
            keyPassword project.signingKeyPassword as String
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes.release {
        runProguard true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        signingConfig signingConfigs.myConfig
    }

    productFlavors {
        nolesfootball {
            applicationId "com.inoles.nolesfootball"
            buildConfigField "String", "NEWS_URL",     "\"http://www.seminoles.com/headline-rss.xml\""
            buildConfigField "String", "ROSTER_URL",   "\"" + makeRosterUrl("fsu") + "\""
            buildConfigField "String", "SCHEDULE_URL", "\"" + makeScheduleUrl("fsu") + "\""
            buildConfigField "String", "TWITTER_URL",  "\"https://twitter.com/Seminoles_com\""
            buildConfigField "String", "FACEBOOK_URL", "\"https://www.facebook.com/FSUSeminoles\""

        }

        knightsfootball {
            applicationId "com.inoles.knightsfootball"
            buildConfigField "String", "NEWS_URL",     "\"http://www.ucfathletics.com/headline-rss.xml\""
            buildConfigField "String", "ROSTER_URL",   "\"" + makeRosterUrl("ucf") + "\""
            buildConfigField "String", "SCHEDULE_URL", "\"" + makeScheduleUrl("ucf") + "\""
            buildConfigField "String", "TWITTER_URL",  "\"https://twitter.com/UCFKnights\""
            buildConfigField "String", "FACEBOOK_URL", "\"https://www.facebook.com/ucfknights\""
        }
    }

    lintOptions {
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        //lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
    }
}

android.applicationVariants.all { variant ->
    variant.outputFile = file("$project.buildDir/${variant.name}.apk")
}

dependencies {
    provided 'com.android.support:support-annotations:+'
    compile 'com.android.support:support-v4:+'
    compile project(':volley')
    //compile 'com.google.code.gson:gson:+'
}
