buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:+'
    compile 'com.koushikdutta.ion:ion:+'
    compile 'com.intellij:annotations:+'
}

def makeRosterUrl(url) {
    return "http://grfx.cstv.com/schools/" + url + "/data/xml/roster/m-footbl-2012.xml"
}

def makeScheduleUrl(url) {
    return "http://cbs-amp2.silverchalice.co/cbs/api/events?schoolCode=" + url + "&sportCode=m-footbl"
}

def makeTwitterUrl(sn) {
    return "https://api.twitter.com/1.1/statuses/user_timeline.json?screen_name=" + sn + "&count=20"
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19

        buildConfigField "String", "TWITTER_KEY", project.twitterConsumerKey
        buildConfigField "String", "TWITTER_SECRET", project.twitterConsumerSecret
    }

    signingConfigs {
        myConfig {
            storeFile file(project.signingStoreLocation)
            storePassword project.signingStorePassword
            keyAlias project.signingKeyAlias
            keyPassword project.signingKeyPassword
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.myConfig
        }
    }

    productFlavors {
        nolesfootball {
            packageName "com.inoles.nolesfootball"
            buildConfigField "String", "NEWS_URL",     "\"http://www.seminoles.com/headline-rss.xml\""
            buildConfigField "String", "ROSTER_URL",   "\"" + makeRosterUrl("fsu") + "\""
            buildConfigField "String", "SCHEDULE_URL", "\"" + makeScheduleUrl("fsu") + "\""
            buildConfigField "String", "TWITTER_URL",  "\"" + makeTwitterUrl("Seminoles_com") + "\""
            buildConfigField "String", "FACEBOOK_URL", "\"https://www.facebook.com/FSUSeminoles\""

        }

        knightsfootball {
            packageName "com.inoles.knightsfootball"
            buildConfigField "String", "NEWS_URL",     "\"http://www.ucfathletics.com/headline-rss.xml\""
            buildConfigField "String", "ROSTER_URL",   "\"" + makeRosterUrl("ucf") + "\""
            buildConfigField "String", "SCHEDULE_URL", "\"" + makeScheduleUrl("ucf") + "\""
            buildConfigField "String", "TWITTER_URL",  "\"" + makeTwitterUrl("UCFKnights") + "\""
            buildConfigField "String", "FACEBOOK_URL", "\"https://www.facebook.com/ucfknights\""
        }
    }

    lintOptions {
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        //lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
    }
}